 ORG 1000H
 PBN2 DW 0CDEFH   ;PARTE BAJA NUMERO 2
 PAN2 DW 89ABH    ;PARTE ALTA NUMERO 2
 PBN1 DW 4567H    ;PARTE BAJA NUMERO 1
 PAN1 DW 0123H    ;PARTE ALTA NUMERO 1
 PBRES DW ?       ;PARTE BAJA RESULTADO
 PARES DW ?       ;PARTE ALTA RESULTADO
 
 
 ORG 3000H
SUM32: PUSH BX    ;GUARDO REGISTROS
 PUSH CX
 PUSH DX
 MOV BX, SP
 ADD BX, 12       ;BX APUNTA A PAN2
                  ;2 POR REGISTRO(6), 2 POR CALL, 2 POR C/OFFSET(4)
 MOV DX, [BX]
 ADD BX, 2        ;BX APUNTA A PAN1
 MOV CX, [BX]
 ADD BX, 2        ;BX APUNTA A PBN2
 MOV AX, [BX]
 ADD BX, 2        ;BX APUNTA A PBN1
 PUSH BX          ;APILA BX PARA PODER USARLO
 MOV BX, [BX]
 
 ADD AX, BX       ;SUMA AMBAS PARTES BAJAS
 ADC CX, DX       ;SUMA AMBAS PARTES ALTAS Y EL FLAG DE CARRY
 POP BX 
 SUB BX, 8        ;BX APUNTA A OFFSET PBRES
                  ;2 POR CADA PARTE DE LOS OPERANDOS (2*4)
 MOV DX, [BX]     ;DX TIENE OFFSET PBRES
 SUB BX, 2        ;BX APUNTA A OFFSET PARES
 MOV BX, [BX]     ;BX TIENE OFFSET PARES
 MOV [BX], CX     ;MUEVE A PARES LA PARTE ALTA DEL RESULTADO
 MOV BX, DX       ;BX TIENE OFFSET PBRES
 MOV [BX], AX     ;MUEVE A PBRES LA PARTE BAJA DEL RESULTADO
 
 POP DX
 POP CX
 POP BX
 RET


 ORG 2000H
 PUSH AX          ;GUARDO AX
 MOV AX, PBN1     ;APILO PARAMETROS
 PUSH AX
 MOV AX, PBN2
 PUSH AX
 MOV AX, PAN1
 PUSH AX
 MOV AX, PAN2
 PUSH AX
 MOV AX, OFFSET PBRES
 PUSH AX
 MOV AX, OFFSET PARES
 PUSH AX
 CALL SUM32
 POP AX          ;DESAPILO PARAMETROS
 POP AX
 POP AX
 POP AX
 POP AX
 POP AX
 POP AX          ;RECUPERO AX
 HLT
 END