programa dos
procesos
  proceso juntarPapeles(ES p:numero)
  comenzar
    mientras ((HayPapelEnLaEsquina) & (p<5))
      tomarPapel
      p:=p+1
  fin
  proceso depositarPapeles
  comenzar
    repetir 5
      depositarPapel
  fin
  proceso checkearSiHaySuficientesPapeles (E p:numero; ES sePudo:boolean)
  variables
    cantPEsq:numero
  comenzar
    cantPEsq:=0
    sePudo:=F
    si HayPapelEnLaEsquina
      mientras ((HayPapelEnLaEsquina)&(cantPEsq<p))
        tomarPapel
        cantPEsq:=cantPEsq+1
      si (cantPEsq=p)
        sePudo:=V
      sino
        repetir cantPEsq
          depositarPapel
  fin
areas
  av5: AreaP(5,1,5,100)
  av10: AreaP(10,1,10,100)
  deposito: AreaC(50,50,50,50)
  arC1: AreaP(11,1,11,1)
  arC2: AreaP(12,1,12,1)
robots
  robot productores
  variables
    p,avOg,caAct:numero
  comenzar
    p:=0
    caAct:=1
    avOg:=PosAv
    mientras (caAct<100)
      juntarPapeles(p)
      si (p=5)
        p:=0
        BloquearEsquina(50,50)
        Pos(50,50)
        depositarPapeles
        Pos(avOg,caAct)
        LiberarEsquina(50,50)
      sino
        mover
        caAct:=PosCa
    juntarPapeles(p)
    si (p=5)
      p:=0
      BloquearEsquina(50,50)
      Pos(50,50)
      depositarPapeles
      Pos(avOg,caAct)
      LiberarEsquina(50,50)
  fin
  robot consumidores
  variables
    p,avOg,caOg,intentosSinExitoSeguidos:numero
    sePudo:boolean
  comenzar
    intentosSinExitoSeguidos:=0
    avOg:=PosAv
    caOg:=PosCa
    mientras (intentosSinExitoSeguidos<8)
      Random(p,2,5)
      BloquearEsquina(50,50)
      Pos(50,50)
      checkearSiHaySuficientesPapeles(p,sePudo)
      Pos(avOg,caOg)
      LiberarEsquina(50,50)
      si sePudo
        intentosSinExitoSeguidos:=0
        repetir p
          depositarPapel
      sino
        intentosSinExitoSeguidos:=intentosSinExitoSeguidos+1
  fin
variables
  p1,p2:productores
  c1,c2:consumidores
comenzar
  AsignarArea(p1,av5)
  AsignarArea(p2,av10)
  AsignarArea(p1,deposito)
  AsignarArea(p2,deposito)
  AsignarArea(c1,arC1)
  AsignarArea(c2,arC2)
  AsignarArea(c1,deposito)
  AsignarArea(c2,deposito)
  Iniciar(p1,5,1)
  Iniciar(p2,10,1)
  Iniciar(c1,11,1)
  Iniciar(c2,12,1)
fin







