ORG 1000H
 NUM_BAJA DW 1000000000111100B
 NUM_ALTA DW 0110111110000100B
 EX DB ?
 MANTISA DB ?
ORG 3000H
INFO_IEEE754:MOV DX, AX
 AND DX, 0111111110000000B ; SACO EL EXPONENTE
 ADD DX, DX                ; ACOMODO EL EXPONENTE A 1B
 MOV CH, DH                ; GUARDO EL EXPONENTE EN CH
 MOV DX, AX                ; PONGO LA PARTE BAJA DEL NRO EN DX
 AND DX, 0000000001111111B ; SACO LA PARTE QUE SUPONE LA MANTISA GUARDADA EN LA PARTE ALTA Y QUEDA EN DL
RET
 
 
INFO_EX: CMP CH, 0          ; ME FIJO SI EL EXPONENTE ES NULO
 JNZ EXP_DIST_CERO          ; SI ES 0 ENTONCES SIGO, SINO SALTO
  MOV EX, 0                 ; GUARDO UN VALOR (0) QUE DENOTA QUE EL EXPONENTE ES NULO
 RET
 EXP_DIST_CERO: CMP CH, 255 ; ME FIJO SI EL EXPONENTE ES 255
 JZ EXPONENTE_TODOS_UNOS    ; SI ES 0 SIGO, SINO SALTO
  MOV EX, 2                 ; GUARDO UN VALOR (2) QUE DENOTA QUE EL EXPONENTE NO ES 255 NI ES NULO
 RET
 EXPONENTE_TODOS_UNOS: MOV EX, 1 ; GUARDO UN VALOR (1) QUE DENOTA QUE EL EXPONENTE ES 255
RET
 
 
INFO_MANTISA: CMP DL, 0     ; ME FIJO SI LA PARTE ALTA DE LA MANTISA ES NULA
 JNZ MANTISA_DIST_CERO      ; SI NO ES CERO SALTO, SI ES CERO LA MANTISA TODAVIA PUEDE SER NO NULA (DEPENDIENDO EL VALOR DE LA PARTE ALTA DE LA MANTISA)
  CMP BX, 0                 ; ME FIJO SI LA PARTE B DE LA MANTISA ES NULA
  JNZ MANTISA_DIST_CERO     ; SI NO ES CERO SALTO, SI ES 0 SIGO
   MOV MANTISA, 0           ; GUARDO UN VALOR (0) QUE DENOTA LA MANTISA ES NULA
 RET
 MANTISA_DIST_CERO: MOV MANTISA, 1 ; GUARDO UN VALOR (1) QUE DENOTA LA MANTISA NO ES NULA
RET
 
INFO_NUMERO: MOV DH, EX    ; GUARDO LA INFO DEL EXPONENTE EN DH
 MOV DL, MANTISA           ; GUARDO LA INFO DE LA MANTISA EN DL
 CMP DH, 0                 ; ME FIJO SI EL EXPONENTE ES NULO
 JZ EX_CERO                ; SI ES 0 SALTO, SINO SIGO
  CMP DH, 1                ; ME FIJO SI EL EXPONENTE ES 1
  JZ EX_255                ; SI ES 255 (EX=1) SALTO, SINO SIGO
   MOV CL, 4               ; COMO EL EXPONENTE NO ES NI 0 NI 255 (EX=2), GUARDO EN CL UN 4 (NRO NORMALIZADO)
  RET
  EX_255: CMP DL, 0        ; COMO EL EXPONENTE ES 255, ME FIJO SI LA MANTISA ES NULA
  JZ MANTISA_NULA          ; SI ES 0 SALTO, SINO SIGO
   MOV CL, 2               ; COMO EL EXPONENTE ES 255 Y LA MANTISA NO ES NULA, GUARDO EN CL UN 2 (NaN)
  RET
  MANTISA_NULA: MOV CL, 1  ; COMO EL EXPONENTE ES 255 Y LA MANTISA ES NULA, GUARDO EN CL UN 1 (+/-INF)
  RET
  
 EX_CERO: CMP DL, 0        ; COMO EL EXPONENTE ES 0, ME FIJO SI LA MANTISA ES NULA
  JZ MANTISA_0             ; SI ES 0 SALTO, SINO SIGO
   MOV CL, 0               ; COMO EL EXPONENTE ES 0 Y LA MANTISA NO ES NULA, GUARDO EN CL UN 0 (NRO NO NORMALIZADO)
  RET
  MANTISA_0: MOV CL, 3     ; COMO EL EXPONENTE ES 0 Y LA MANTISA ES NULA, GUARDO EN CL UN 3 (+/-CERO)
RET


ORG 2000H
 MOV AX, NUM_ALTA
 MOV BX, NUM_BAJA
 CALL INFO_IEEE754
 CALL INFO_EX
 CALL INFO_MANTISA
 CALL INFO_NUMERO
 HLT 
END